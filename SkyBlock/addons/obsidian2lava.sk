#
# ==============
# obsidian2lava.sk v0.0.3
# ==============
# Let users revert obsidian back to lava.
# ==============
# Dependencies
# ==============
# > Spigot 1.13.2 - https://hub.spigotmc.org/jenkins/job/BuildTools/
# > Skript by bensku - https://github.com/SkriptLang/Skript/releases
# > SKYBLOCK.SK - https://github.com/Abwasserrohr/SKYBLOCK.SK

#
# > Event: on rightclick on a obsidian block while holding a empty bucket
# > Actions:
# > The code below reverts obsidian back to lava, if the player has a empty bucket in his hand.
on rightclick on obsidian with a empty bucket:
  #
  # > Check if the player is not allowed to do this action.
  set {_allowed} to checkislandaccess(player,location of event-block)
  if {_allowed} is false:
    cancel event
  #
  # > If the player is allowed, change the obsidian to lava.
  else:
    if player's tool is a empty bucket:
      set event-block to air
      remove 1 of empty bucket from player's inventory
      if player's tool is air:
        set player's tool to lava bucket
        cancel event
      else:
        give 1 of lava bucket to player
        cancel event

#
# > Event: on block form of obsidian
# > This event happens, if water changes lava into obsidian, this is very bad, since lava is
# > necessary to build a cobblestone generator to farm new cobblestone and other stone.
# > Actions:
# > Once this event is triggered, it loops all players on the server and if the distance between
# > the loop-player and event-location is lower than 30, it informs the player about this addon.
on block form of obsidian:
  $ thread
  loop all players:
    if distance between loop-player and event-location < 30:
      set {_lang} to getlangcode(loop-player)
	  set {_prefix} to getlang("prefix",{_lang})
	  set {_msg} to getlang("og_o2lforminfo",{_lang})
      send "%{_prefix}% %{_msg}%" to loop-player
