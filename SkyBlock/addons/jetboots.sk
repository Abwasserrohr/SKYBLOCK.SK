#
# ==============
# jetboots.sk v0.0.12
# ==============
# Let users craft jetboots, which can be used to fly around. They need fuel to fly.
# ==============
# Dependencies
# ==============
# > Spigot - https://hub.spigotmc.org/jenkins/job/BuildTools/
# > Skript by bensku - https://github.com/SkriptLang/Skript/releases
# > SKYBLOCK.SK - https://github.com/Abwasserrohr/SKYBLOCK.SK
# ==============
# How to use it:
# ==============
# > This skript doesn't need any setup on server side.
# > First use: Place jetboots.sk into your "plugins/Skript/scripts/" folder and restart. Subfolders are possible too.
# > Steps for users:
# > Craft this recipe:
# >      air      |   air   |      air
# > diamond block | feather | diamond block
# > diamond block | feather | diamond block
# > Wear the shoes and then fly around, you may
# > have to fill the up with coal while waring them.
# > Simply click with coal in your hand to fill them up.

#
# > If you change these options, might be necessary,
# > JetBoots, which are already in the game are not
# > going to work anymore, if you change the item_name.
options:
  #
  # > Defines how the item should be named:
  item_name: "&eJetBoots"
  #
  # > Fuel items, set all items which should be usable as a fuel, separate with "or".
  # > Set the fuel efficiency of each fuel type below at the on load event.
  fuelitems: coal or charcoal
  #
  # > Defines the speed of the boots.
  flyspeed: 0.04
  #
  # > Default translation, if no SkyBlock translation is available.
  fillinfo: "Fill &eJetBoots&r up with coal while wearing them."
  emptyinfo: "Your &eJetBoots&r are empty!"
  loadedinfo: "Your &eJetBoots&r have been filled up."
  actionbar: &8[&6&lJetBoots&8] &8>> [&r<fuel>&8]
  #
  # > Set a number once the player should get a warn color on the fuel,
  # > which tells the player that the jetboots are going to be empty soon.
  fuelwarn: 500

#
# > Event - on load
# > Action:
# > Once this file is loaded by skript, the JetBoots get registered as a crafting recipe.
# > Also, if there is a old jetboots list left, delete it.
on load:
  #
  # > Wait a second until needed functions are loaded.
  wait 1 second
  #
  # > Sets the efficiency for coal.
  loadconfigobject("JB-fuelefficiency-coal",20)
  #
  # > Sets the efficiency for coal.
  loadconfigobject("JB-fuelefficiency-charcoal",5)
  set {_item} to diamond boots of mending
  set name of {_item} to {@item_name}
  set line 1 of lore of {_item} to {@item_name}
  set line 2 of lore of {_item} to "&r0"
  wait 20 seconds
  addShapedRecipe({_item},air, air, air, diamond block, feather, diamond block, diamond block, feather, diamond block)

#
# > Event - on craft
# > Action:
# > If the player has crafted a recipe which contains the defined item name
# > in the first line of the lore, give the player information on how to fill them.
on craft:
  if line 1 of lore of event-item is {@item_name}:
    set {_lang} to getlangcode(player)
    #
    # > Print predefined message, if no translation is available.
    if getlang("jetboots_fi",{_lang}) is not set:
      message {@fillinfo}
    else:
      set {_prefix} to getlang("prefix",{_lang})
      set {_msg} to getlang("jetboots_fi",{_lang})
      message "%{_prefix}% %{_msg}%"

#
# > Import Java classes from bukkit to use particle effects for the JetBoots in the jetparticle function.
import:
  org.bukkit.Particle
  org.bukkit.Effect

#
# > Event - on gamemode change to survival
# > Actions:
# > If the player is wearing the jetboots and the gamemode changes,
# > reapply the jetboots functions.
on gamemode change to survival:
  if line 1 of lore of player's boots is {@item_name}:
    jetboots(player)

#
# > Event - on inventory click
# > Actions:
# > If the player clicks and the event item is either diamond boots or the
# > event-slot is the boots slot, check for the lore of the boots the
# > player is currently wearing, if they're valid jetboots, call the
# > jetboots function.
on inventory click:
  if event-item is diamond boots:
    set {_check} to true
  else if event-slot is player's boots:
    set {_check} to true
  else if event.isShiftClick() is true:
    set {_check} to true
  if {_check} is true:
    wait 2 ticks
    if line 1 of lore of player's boots is {@item_name}:
      jetboots(player)

#
# > Event - on quit
# > Actions:
# > If the player leaves the server, delete the metadata value for the jetboots
# > from the player to stop while loops.
on quit:
  delete metadata value "jetboots" of player

#
# > Event - on join
# > Actions:
# > If the player is joining and wearing jetboots, call jetboots function.
on join:
  wait 5 seconds
  if line 1 of lore of player's boots is {@item_name}:
    jetboots(player)

#
# > Event on rightclick holding diamond boots
# > Actions:
# > If the player is rightclicking with diamond boots,
# > check if these are jetboots, if they are, call the
# > jetboots function.
on rightclick holding diamond boots:
  wait 1 tick
  if line 1 of lore of player's boots is {@item_name}:
    jetboots(player)

#
# > Function - jetboots
# > Parameters:
# > <player>the player who is wearing jetboots
# > Actions:
# > Allows the player to fly with the jetboots. Stops the flying automatically
# > if the player has no fuel or removes the jetboots from the boots inventory slot.
function jetboots(player:player):
  #
  # > The uuid of the player is needed for messages.
  set {_uuid} to uuid of {_player}
  #
  # > Wait a tick 
  wait 1 tick
  #
  # > Make the player fly at a specified fly speed.
  set {_player}'s flight mode to true
  set {_player}'s fly speed to {@flyspeed}
  #
  # > Create a prefix, since it is needed frequently.
  set {_lang} to {SK::lang::%{_uuid}%}
  set {_prefix} to getlang("prefix",{_lang})
  #
  # > Set the metadata value "jetboots" of the player to the current timestamp,
  # > if the timestamp changes, running while loops are going to stop.
  set {_now} to now
  set metadata value "jetboots" of {_player} to {_now}
  while line 1 of lore of {_player}'s boots is {@item_name}:
    #
    # > If the temporary saved timestamp isn't the one which is set
    # > to the metadata of the player, then stop the while loop.
    if {_now} is not metadata value "jetboots" of {_player}:
      stop loop

    #
    # > Remove fuel from the JetBoots if the player is flying and
    # > update the actionbar of the player.
    if {_player} is flying:
      delete {_lore}
      set {_lore} to line 2 of lore of {_player}'s boots
      # > If there is no lore, stop here.
      if {_lore} is not set:
        set {_player}'s flight mode to false
        set {_player}'s fly speed to 0.08
        delete metadata value "jetboots" of {_player}
        stop

      replace all "&r" with "" in {_lore}
      replace all "&4" with "" in {_lore}
      set {_lore} to {_lore} parsed as integer
      if {_lore} > {@fuelwarn}:
        set {_display} to "{@actionbar}"
        replace all "<fuel>" with "%{_lore}%" in {_display}
        send action bar {_display} to {_player}
      #
      # > Make the actionbar colored red and fat, if it is below the predefined number.
      else:
        set {_display} to "{@actionbar}"
        replace all "<fuel>" with "&4&l%{_lore}%" in {_display}
        send action bar {_display} to {_player}
      if {_lore} <= 0:
        #
        # > If the lore is 0 or lower, print info to the player and stop flying.
        #
        # > Print predefined messages, if no translation is available.
        if getlang("jetboots_ei",{_lang}) is not set:
          message {@emptyinfo} to {_player}
        else:
          set {_msg} to getlang("jetboots_ei",{_lang})
          message "%{_prefix}% %{_msg}%" to {_player}
        if getlang("jetboots_fi",{_lang}) is not set:
          message {@fillinfo} to {_player}
        else:
          set {_msg} to getlang("jetboots_fi",{_lang})
          message "%{_prefix}% %{_msg}%" to {_player}
        set {_player}'s flight mode to false
        set {_player}'s fly speed to 0.08
        delete metadata value "jetboots" of {_player}
        stop
      #
      # > Check if the player is allowed to fly with the JetBoots at the center of the current island.
      set {_allowed} to checkislandaccess({_player},getcurrentbedrock({_player},location of {_player}),"jetboots")
      #
      # > If the player isn't allowed to fly, maybe only because the payer is above or below the
      # > own island, check if the player is on/below y-coordinate 0 or on/above y-coord 255.
      if {_allowed} is false:
        if y-coord of {_p} <= 0:
          set {_allowed} to true
        if y-coord of {_p} >= 255:
          set {_allowed} to true
      if {_allowed} is false:
        set {_player}'s flight mode to false
        set {_lang} to {SK::lang::%{_uuid}%}
        set {_msg} to getlang("flag_errorfalse",{_lang})
        message "%{_prefix}% %{_msg}%" to {_player}
        send action bar "&4%{_msg}%" to {_player}
        wait 1 tick
        set {_player}'s flight mode to true
      #
      # > Spawn some jet particles using the jetparticle function.
      jetparticle({_player})
      remove 1 from {_lore}
      if {_lore} < {@fuelwarn}:
        set line 2 of lore of {_player}'s boots to "&4%{_lore}%"
      else:
        set line 2 of lore of {_player}'s boots to "&r%{_lore}%"
    #
    # > After every while loop, wait 1 tick.
    wait 1 tick

  #
  # > If the while loop ends, disable flying for player,
  # > this happens, if the player takes the JetBoots off while
  # > flying in the air.
  delete metadata value "jetboots" of {_player}
  set {_player}'s flight mode to false
  set {_player}'s fly speed to 0.08

#
# > Function - jetparticle
# > Parameters - <player>player
# > Actions:
# > Spawn particles for JetBoots.
function jetparticle(p:player):
  set {_l} to location of {_p}
  set {_x} to x-coord of {_l}
  set {_y} to y-coord of {_l}
  set {_z} to z-coord of {_l}
  set {_world} to {_p}'s world
  {_world}.spawnParticle(Particle.EXPLOSION_NORMAL!,{_x},{_y},{_z}, 0.5)

#
# > Event - on rightclick holding coal
# > Actions:
# > If the player is rightclicking while holding coal and the player is wearing JetBoots,
# > the coal gets removed from the inventory of the player and added as fuel to the JetBoots.
on rightclick holding {@fuelitems}:
  if player's boots is set:
    if line 1 of lore of player's boots is {@item_name}:
      set {_lore} to line 2 of lore of player's boots
      #
      # > Replace all the formatting for the lore.
      replace all "&r" with "" in {_lore}
      replace all "&4" with "" in {_lore}
      #
      # > Parse the string to an integer.
      set {_lore} to {_lore} parsed as integer
      #
      # > Get the tool to check for the size of the ItemStack the player
      # > is currently holding.
      set {_tool} to player's tool
      set {_amount} to {_tool}.getAmount()
      #
      # > Removes the fuel by replacing it with air.
      set player's tool to air
      #
      # > Get the fuel efficiency for the current fuel.
      set {_fuelefficiency} to getconfigobject("JB-fuelefficiency-%1 of {_tool}%")
      set {_fuelbonus} to {_amount} * {_fuelefficiency}
      add {_fuelbonus} to {_lore}
      if {_lore} < {@fuelwarn}:
        set line 2 of lore of player's boots to "&4%{_lore}%"
      else:
        set line 2 of lore of player's boots to "&r%{_lore}%"
      set {_lang} to getlangcode(player)
      #
      # > Print predefined message, if no translation is available.
      if getlang("jetboots_li",{_lang}) is not set:
        message {@loadedinfo}
      else:
        set {_prefix} to getlang("prefix",{_lang})
        set {_msg} to getlang("jetboots_li",{_lang})
        message "%{_prefix}% %{_msg}%"
