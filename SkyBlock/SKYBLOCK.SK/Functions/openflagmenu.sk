#
# ==============
# openflagmenu.sk
# ==============
# openflagmenu.sk is part of the SKYBLOCK.SK functions.
# ==============

# > Function - openflagmenu:
# > Arguments:
# > <player>player
# > Actions:
# > Opens the flags for the player to change, if enabled in config.sk
function openflagmenu(p:player,type:text):
  #
  # > Get the uuid of the player and set it to a local variable.
  set {_uuid} to uuid of {_p}

  #
  # > Get the language of the player in a local variable.
  set {_lang} to {SK::lang::%{_uuid}%}
  
  #
  # > If there is no type set, open a menu where the player can select, on which type of
  # > player the flags should apply. The player can select between foreign, trusted and island member.
  if {_type} is "":
    opengui({_p},27,"%{SB::lang::flag::flagst::%{_lang}%}%",chest inventory)
    #
    # > Empty slots are shown as black stained glass.
    loop 27 times:
      set slot loop-number - 1 of {_p}'s current inventory to black stained glass pane named " "

    setguiitem({_p},11,farmland,1,"&r%{SB::lang::flag::foreignt::%{_lang}%}%","&r%{SB::lang::flag::foreignd::%{_lang}%}%","openflagmenu(""%{_p}%"" parsed as player,""foreign"")",false)
    setguiitem({_p},13,grass path,1,"&r%{SB::lang::flag::trustedt::%{_lang}%}%","&r%{SB::lang::flag::trustedd::%{_lang}%}%","openflagmenu(""%{_p}%"" parsed as player,""trusted"")",false)
    setguiitem({_p},15,grass,1,"&r%{SB::lang::flag::membert::%{_lang}%}%","&r%{SB::lang::flag::memberd::%{_lang}%}%","openflagmenu(""%{_p}%"" parsed as player,""member"")",false)
    #
    # > This item allows the user to go into the previous menu.
    setguiitem({_p},26,{SB::config::backguiitem},1,"&r%{SB::lang::guibacktopreviousmenu::%{SK::lang::%{_uuid}%}%}%","%{SB::lang::guibacktopreviousmenulore::%{SK::lang::%{_uuid}%}%}%","make ""%{_p}%"" parsed as player execute command ""/is""",true)
    stop

  #
  # > Set the prefix for messages into the local variable {_prefix}.
  set {_prefix} to {SB::lang::prefix::%{_lang}%}

  #
  # > Get the bedrock of the player who executed the command.
  set {_bedrock} to {SB::player::%{_uuid}%::island::bedrock}
  #
  # > Get the x, y and z-coordinates of the bedrock to use the variables.
  set {_x} to x-coordinate of {_bedrock}
  set {_y} to y-coordinate of {_bedrock}
  set {_z} to z-coordinate of {_bedrock}
  #
  # > Check, if the player who executed the command is also the leader of the island.
  if {SB::island::%{_x}%_%{_y}%_%{_z}%::leader} is {_uuid}:
    #
    # > Set the slots to a default size of 54.
    set {_slots} to 54
    #
    # > Open the flags menu.
    opengui({_p},{_slots},"&l%{SB::lang::flag::%{_type}%t::%{_lang}%}%")
    #
    # > Empty slots are shown as black stained glass.
    loop {_slots} times:
      set slot loop-number - 1 of {_p}'s current inventory to black stained glass pane named " "

    #
    # > Set {_i} to 10, to order the items in the menu right.
    set {_i} to 10
    loop {SB::config::defaultisflag::%{_type}%::*}:

      #
      # If the custom flag is set, set it to a local variable, if not, take the default configuration.
      if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_type}%::%loop-index%} is set:
        set {_boolean} to {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_type}%::%loop-index%}
      else:
        set {_boolean} to loop-value
      #
      # > Set a lore which is added to the menu item.
      if {SB::config::toggleflags} is true:
        set {_lore} to "%{SB::lang::flag::%loop-index%d::%{_lang}%}%\n%{SB::config::guispacer}%\n%{SB::lang::flag::currentsetting::%{_lang}%}%%{SB::lang::flag::current%{_boolean}%::%{_lang}%}%\n%{SB::config::guispacer}%\n%{SB::lang::flag::toggleflagstrue::%{_lang}%}%"
      else:
        set {_lore} to "%{SB::lang::flag::%loop-index%d::%{_lang}%}%\n%{SB::config::guispacer}%\n%{SB::lang::flag::currentsetting::%{_lang}%}%%{SB::lang::flag::current%{_boolean}%::%{_lang}%}%\n%{SB::config::guispacer}%\n%{SB::lang::flag::toggleflagsfalse::%{_lang}%}%"

      #
      # > Set either green glass panes or red glass panes for true and false and then set the item.
      # > The items have translation text as variable and also execute the "toggleflag" function onn click.
      if {_boolean} is true:
        setguiitem({_p},{_i},green glass pane,1,"&r%{SB::lang::flag::%loop-index%t::%{_lang}%}%","&r%{_lore}%","toggleflag(""%{_p}%"" parsed as player,""%loop-index%"",""%{_type}%"")",false)
      if {_boolean} is false:
        setguiitem({_p},{_i},red glass pane,1,"&r%{SB::lang::flag::%loop-index%t::%{_lang}%}%","&r%{_lore}%","toggleflag(""%{_p}%"" parsed as player,""%loop-index%"",""%{_type}%"")",false)
      #
      # > Add 1 to {_i} to place the items in the slots in the right order.
	  # > If the loop amount is over 7, add 2 to {_i} and set tthe loop amount back to 0.
      add 1 to {_loopamount}
      if {_loopamount} is 7:
        set {_loopamount} to 0
        add 3 to {_i}
      else:
        add 1 to {_i}
    #
    # > Set the back item to let the player go back to the previous menu.
    setguiitem({_p},53,{SB::config::backguiitem},1,"&r%{SB::lang::guibacktopreviousmenu::%{SK::lang::%{_uuid}%}%}%","%{SB::lang::guibacktopreviousmenulore::%{SK::lang::%{_uuid}%}%}%","make ""%{_p}%"" parsed as player execute command ""/is flags""",true)
  else:
    message "%{_prefix}% %{SB::lang::notleader::%{_lang}%}%" to {_p}

# > Function - toggleflag:
# > Arguments:
# > <player>player,<text>flag,<text>type (foreign,trusted,member)
# > Actions:
# > Toggles a flagg for the specific type of player.
function toggleflag(p:player, flag:text, type:text):
  if {SB::config::toggleflags} is true:
    #
    # > Get the uuid of the player and set it to a local variable.
    set {_uuid} to uuid of {_p}
    #
    # > Get the bedrock of the player who executed the command.
    set {_bedrock} to {SB::player::%{_uuid}%::island::bedrock}
    #
    # > Get the x, y and z-coordinates of the bedrock to use the variables.
    set {_x} to x-coordinate of {_bedrock}
    set {_y} to y-coordinate of {_bedrock}
    set {_z} to z-coordinate of {_bedrock}

    #
    # > Get the current flag setting.
    set {_flagsetting} to {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_type}%::%{_flag}%}
    
    #
    # > If the flag setting is not set, use the default setting.
    if {_flagsetting} is not set:
      set {_flagsetting} to {SB::config::defaultisflag::%{_type}%::%{_flag}%}
    #
    # > Toggle the setting.
    if {_flagsetting} is true:
      set {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_type}%::%{_flag}%} to false
    if {_flagsetting} is false:
      set {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_type}%::%{_flag}%} to true
  #
  # > Reopen the flag menu.
  openflagmenu({_p},{_type})
