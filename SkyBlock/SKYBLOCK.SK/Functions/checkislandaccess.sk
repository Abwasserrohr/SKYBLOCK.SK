#
# ==============
# checkislandaccess.sk
# ==============
# checkislandaccess.sk is part of the SKYBLOCK.SK functions.
# ==============

# > Function - checkislandaccess:
# > Arguments:
# > <player>player, <location>location,[<integer>integer]
# > Actions:
# > Going to check if the player is allowed to build at the defined location, this
# > calls getcurrentbedrock, which is searching for the right bedrock and then
# > checks if the player is allowed to build there and then returns either true (allowed)
# > or false (not allowed).
function checkislandaccess(p:player,l: location, event:text=1, block:item=air, t: integer=1) :: boolean:
  #
  # > Call the function which generates new temporary variables and sets them
  # > to the current border and also returns the bedrock location.
  set {_bedrock} to getcurrentbedrock({_p},{_l})
  #
  # > If this is not a valid island, check trough default lobby flags:
  if {_bedrock} is not set:
    if {_event} is "Explode":
      return {SB::config::defaultlobbyflag::explosion}
    if {_p} has permission "is.admin":
      return true
    if {_event} is "PlayerInteractEvent":
      if {_block} is lever or button:
        return {SB::config::defaultlobbyflag::button}
      if {_block} is door:
        return {SB::config::defaultlobbyflag::door}
    else if {_event} is "EntityDamageByEntityEvent-animal":
      return {SB::config::defaultlobbyflag::animal-damage}
    else if {_event} is "EntityDamageByEntityEvent-monster":
      return {SB::config::defaultlobbyflag::mob-damage}
    else if {_event} is "VehicleDamageEvent":
      return {SB::config::defaultlobbyflag::interact-vehicle}
    else if {_event} is "jetboots":
      return {SB::config::defaultlobbyflag::jetboots}


  #
  # > The getcurrentbedrock function created some global temporary variables we can now check,
  # > if the location parameter is within the temporary variables, we can use them.
  if {_l} is within metadata value "TEMPLOCb1" of {_p} to metadata value "TEMPLOCb2" of {_p}:
    set {_uuid} to uuid of {_p}
    set {_x} to x-coord of {_bedrock}
    set {_y} to y-coord of {_bedrock}
    set {_z} to z-coord of {_bedrock}

    #
    # > Check for permissions:
    if {_p} has permission "is.admin":
      #
      # > Check if the explosion should happen or not.
      if {_event} is "Explode":
        set {_check} to checkflagevent({_x},{_y},{_z},{_event},"foreign",{_block})
        return {_check}
      return true
    #
    # > The leader of the island is allowed to build
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::leader} is {_uuid}:
      #
      # > Check if the explosion should happen or not.
      if {_event} is "Explode":
        set {_check} to checkflagevent({_x},{_y},{_z},{_event},"member",{_block})
        if {_check} is set:
          return {_check}
      return true
    else:
      #
      # > Loop trough members, if the player is a member, return true.
      loop {SB::island::%{_x}%_%{_y}%_%{_z}%::member::*}:
        #
        # > Check if the user is a member, if it is, return the result of the function getcurrentbedrockmain.
        if "%loop-value%" is "%{_uuid}%":
          #
          # > Flag check for trusted players:
          set {_check} to checkflagevent({_x},{_y},{_z},{_event},"member",{_block})
          if {_check} is set:
            return {_check}
          #
          # > If no return happened for a flag, return true for members.
          return true
      #
      # > Loop trough trusted players, if the player is trusted, return true.
      loop {SB::island::%{_x}%_%{_y}%_%{_z}%::trust::*}:
        #
        # > Check if the user is a trusted player, return the result of the function getcurrentbedrockmain.
        if "%loop-value%" is "%{_uuid}%":
          #
          # > Flag check for trusted players:
          set {_check} to checkflagevent({_x},{_y},{_z},{_event},"trusted",{_block})
          if {_check} is set:
            return {_check}
          #
          # > If no return happened for a flag, return true for trusted players.
          return true
          
      # 
      # > Flag check for foreign players (other islands).
      # > Check if there is a flag which allows specific events:
      set {_check} to checkflagevent({_x},{_y},{_z},{_event},"foreign",{_block})
      if {_check} is set:
        return {_check}
      #
      # > If there hasn't been a return yet, return false now, since the player has no
      # > right to do this action here. Also check if there are upgrades available to
      # > give the player access to the island.
      if {_t} is not 5:
        checkforupgrades({_p},{_bedrock})
      return false
  #
  # > The locations aren't match, go forward and do another check, if that's also not
  # > working, return false.
  else:
    #
    # > If the player is a admin, return true.
    if {_p} has permission "is.admin":
      return true
    if {_t} is 1:
      return checkislandaccess({_p}, {_l}, {_event}, {_block}, 2)
    else:
      #
      # > The player isn't allowed, return false. Also check if there
      # > are upgrades which could give the player access too this area.
      if {_t} is not 5:
        checkforupgrades({_p},{_bedrock})
      return false

#
# > Function - checkforupgrades:
# > Parameters:
# > <player>player
# > <location>bedrock location of the current island
# > Actions:
# > If the player would be allowed on the island but the island is not fully upgraded,
# > send a information that the island can be upgraded.
function checkforupgrades(player:player,bedrock:location):
  set {_allowed} to checkislandaccess({_player},{_bedrock},"build",air,5)
  if {_allowed} is true:
    set {_lang} to getlangcode({_player})
    set {_prefix} to getlang("prefix",{_lang})
    set {_msg} to getlang("upgradeavailable",{_lang})
    message "%{_prefix}% %{_msg}%" to {_player}

#
# > Function - getcurrentbedrock:
# > Arguments:
# > <player>player, <location>location
# > Actions:
# > Generatres temporary variables which store the current borders of the player,
# > if the temporary variables are set, it returns them if everything is ok.
function getcurrentbedrock(p:player, l:location) :: location:
  if metadata value "TEMPLOC" of {_p} is not set:
    set {_new} to true
  if metadata value "TEMPLOCb1" of {_p} is not set:
    set {_new} to true
  if metadata value "TEMPLOCb2" of {_p} is not set:
    set {_new} to true
  if {_new} is true:
    #
    # > Get the current bedrock of the player,
    # > this takes around 0.02 seconds, thats why we use caching here.
    set {_bedrock} to getcurrentbedrockmain({_l})

    #
    # > Generate the new borders of the current bedrock
    set {_pl::1} to location of {_bedrock}
    set {_pl::2} to location of {_bedrock}
    
    #
    # > Calculate the upgradeable border:
    set {_defaultdist} to {SB::config::defaultsize}
    set {_maxdist} to {SB::config::distance}
    set {_dist} to {SB::config::distance}
    
    #
    # > Only calculate, if there needs to be a upgradeable border calculation:
    if {_maxdist} is not {_defaultdist}:
      #
      # > Get the coordintaes to then get the current size level of the island.
      set {_x} to x-coord of {_bedrock}
      set {_y} to y-coord of {_bedrock}
      set {_z} to z-coord of {_bedrock}
      set {_lvl} to {SB::island::%{_x}%_%{_y}%_%{_z}%::sizeupgrade}
      #
      # > Calculate how much we can upgrade the island until the maximum is reached.
      set {_upgradeabledist} to ({_maxdist} - {_defaultdist})
      #
      # > Calculate how much we can increase per level, we have default 5 levels to unlock.
      set {_distincreaseperlevel} to ({_upgradeabledist} / 5)
      #
      # > Calculate the current size bonus using the calculated increase per level + the current size level.
      set {_currentdistsizebonus} to ({_lvl} * {_distincreaseperlevel})
      #
      # > Add the size bonus by current size level to the distance of the island.
      set {_dist} to ({_defaultdist} + {_currentdistsizebonus})
    
    add ({_dist} - {SB::config::protect}) to x-coord of {_pl::1}
    add ({_dist} - {SB::config::protect}) to z-coord of {_pl::1}
    subtract ({_dist} - {SB::config::protect}) from x-coord of {_pl::2}
    subtract ({_dist} - {SB::config::protect}) from z-coord of {_pl::2}
    set y-coordinate of {_pl::2} to 256
    set y-coordinate of {_pl::1} to 0
    
    #
    # > Set border 1 location
    set metadata value "TEMPLOCb1" of {_p} to {_pl::1}
    #
    # > Set border 2 location
    set metadata value "TEMPLOCb2" of {_p} to {_pl::2}
    #
    # > Set the location of the bedrock
    set metadata value "TEMPLOC" of {_p} to location of {_bedrock}
    #
    # > Returns the bedrock location
    return {_bedrock}
  else:
    #
    # > If the temporary location is usable and within the location, return it
    if {_l} is within metadata value "TEMPLOCb1" of {_p} to metadata value "TEMPLOCb2" of {_p}:
      return metadata value "TEMPLOC" of {_p}
    #
    # > If the temporary location seems outdated, delete it and repeat.
    else:
      delete metadata value "TEMPLOC" of {_p}
      delete metadata value "TEMPLOCb1" of {_p}
      delete metadata value "TEMPLOCb2" of {_p}
      return getcurrentbedrock({_p},{_l})
#
# > Function - handlegeneralflagevent:
# > Arguments:
# > <event>the event
# > <boolean>the boolean required to cancel the event
# > <location>location
# > <text>flag
# > <item>the trigger block
# > Actions:
# > Checks the general flag of an island and cancels it if it meets the required state.
function handlegeneralflagevent(event:object,cancel:boolean,l:location,flag:text,block:item=air):
  set {_bedrock} to getcurrentbedrockmain({_l})
  set {_flag} to checkflagevent(x-coord of {_bedrock},y-coord of {_bedrock},z-coord of {_bedrock},"%{_flag}%","general",{_block})
  if {_flag} is {_cancel}:
    {_event}.setCancelled(true)

#
# > Function - getcurrentbedrockmain:
# > Arguments:
# > <location>location
# > Actions:
# > Gets the bedrock location of the location parameter and returns the location.
function getcurrentbedrockmain(l:location) :: location:
  #
  # > Calculate the location of the bedrock and return it.
  # > Formular: x of island center: x of event location / (distance*2)*distance*2+0.5
  set {_d2} to {SB::config::distance}*2
  set {_x} to (round((x-coord of {_l}) / ({_d2}))*{_d2})
  set {_z} to (round((z-coord of {_l}) / ({_d2}))*{_d2})
  #
  # > Set and return the valid bedrock location, which is used by SKYBLOCK.SK.
  set {_loc} to location of block at location at {_x}, {SB::config::height}, {_z} in "%{_l}'s world%" parsed as world
  return {_loc}

#
# > Function - checkflagevent:
# > Arguments:
# > <text>x coordinate of island,<text>y coordinate of island,<text>z coordinate of island,<text>event,<text>flag,<item>block
# > Actions:
# > Returns if a flag is set to true or false for a specific event and playertype (foreign, trusted, member).
function checkflagevent(x:number,y:number,z:number,event:text,playertype:text,block:item) :: boolean:
  #
  # > This flag is for all kinds of interactions, like switching buttons or levers, opening/closing doors or looking into chests.
  if {_event} is "PlayerInteractEvent":
    if {_block} is lever or button:
      if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::button} is set:
        return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::button}
      else:
        if {SB::config::defaultisflag::%{_playertype}%::button} is set:
          return {SB::config::defaultisflag::%{_playertype}%::button}
    if {_block} is door:
      if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::door} is set:
        return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::door}
      else:
        if {SB::config::defaultisflag::%{_playertype}%::door} is set:
          return {SB::config::defaultisflag::%{_playertype}%::door}
    if {_block} is any chest or trapped chest or dropper or dispenser or shulker box or brewing stand or hopper or anvil or enchantment table or furnace:
      if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::chest} is set:
        return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::chest}
      else:
        if {SB::config::defaultisflag::%{_playertype}%::chest} is set:
          return {SB::config::defaultisflag::%{_playertype}%::chest}
  #
  # > This is for animals like pigs and cows.
  else if {_event} is "EntityDamageByEntityEvent-animal":
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::animal-damage} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::animal-damage}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::animal-damage} is set:
        return {SB::config::defaultisflag::%{_playertype}%::animal-damage}
  #
  # > This is for entities like armor stands, villagers and item frames.
  else if {_event} is "EntityDamageByEntityEvent-entity":
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::entityclick} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::entityclick}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::entityclick} is set:
        return {SB::config::defaultisflag::%{_playertype}%::entityclick}
  #
  # > This is for monsters like zombies.
  else if {_event} is "EntityDamageByEntityEvent-monster":
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::mob-damage} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::mob-damage}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::mob-damage} is set:
        return {SB::config::defaultisflag::%{_playertype}%::mob-damage}
  #
  # > This flag is for vehicles like minecarts and boats.
  else if {_event} is "VehicleDamageEvent" or "vehicle-click":
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::interact-vehicle} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::interact-vehicle}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::interact-vehicle} is set:
        return {SB::config::defaultisflag::%{_playertype}%::interact-vehicle}
  #
  # > This flag is for explosions from creepers and tnt.
  else if {_event} is "Explode":
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::explosion} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::explosion}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::explosion} is set:
        return {SB::config::defaultisflag::%{_playertype}%::explosion}
  #
  # > This flag is for redstone tools, like comparators and repeaters.
  else if {_event} is "Redstone":
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::redstone} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::redstone}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::redstone} is set:
        return {SB::config::defaultisflag::%{_playertype}%::redstone}
  #
  # > This flag is for the public home trough /island home [<player>] [<home>]
  else if {_event} is "Home":
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::publichome} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::publichome}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::publichome} is set:
        return {SB::config::defaultisflag::%{_playertype}%::publichome}
  #
  # > This flag is for clicks on entities, like villagers.
  else if {_event} is "entity-click":
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::entityclick} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::entityclick}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::entityclick} is set:
        return {SB::config::defaultisflag::%{_playertype}%::entityclick}
  #
  # > There seems to be no predefined flag hardcoded into this file, check variables.
  else:
    if {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::%{_event}%} is set:
      return {SB::island::%{_x}%_%{_y}%_%{_z}%::flag::%{_playertype}%::%{_event}%}
    else:
      if {SB::config::defaultisflag::%{_playertype}%::%{_event}%} is set:
        return {SB::config::defaultisflag::%{_playertype}%::%{_event}%}
