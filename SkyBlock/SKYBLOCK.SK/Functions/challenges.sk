#
# ==============
# challenges.sk
# ==============
# challenges.sk is part of the SKYBLOCK.SK functions.
# ==============

#
# > Import needed Java classes for the openchallenges function.
import:
  java.text.SimpleDateFormat
  java.util.Date

#
# > Function: openchallenges
# > Arguments:
# > <player>player
# > <number>the level page which should be opened (default = 0|stats site)
# > Actions:
# > Opens the challenges menu, which allows the players to solve
# > challenges through 5 levels.
function openchallenges(p:player,page:number=0):
  #
  # > Start at slot 0.
  set {_slot} to 0
  #
  # > Start at level 1.
  set {_challengelvl} to 1
  #
  # > Set the background item as a variable
  set {_backgrounditem} to black stained glass pane named " "
  #
  # > Get local varibales we need, like uuid and language.
  set {_uuid} to uuid of {_p}
  set {_lang} to getlangcode({_p})
  set {_prefix} to getlang("prefix",{_lang})
  #
  # > If the challenges are disabled, print error and stop here.
  if {SB::config::usechallenges} is false:
    set {_msg} to getlang("challenge_disabled",{_lang})
    message "%{_prefix}% %{_msg}%" to {_p}
    stop
  #
  # > Get the date and check, if the day is over,
  # > reset repeat counters.
  set {_date} to new Date()
  set {_d} to new SimpleDateFormat("yyyy-MM-dd").format({_date})
  if {SB::today} is not {_d}:
    set {SB::today} to {_d}
    delete {SB::crepeats::*}
  #
  # > Open the menu to the player if it is not already opened.
  if name of {_p}'s current inventory is not getlang("challenge_menutitle",{_lang}):
    opengui({_p},54,getlang("challenge_menutitle",{_lang}))
  loop 54 times:
    set slot loop-number - 1 of {_p}'s current inventory to 1 of {_backgrounditem}
    protectslot({_p},loop-number - 1)
  #
  # > To process already finished challenges and statistics, we need the bedrock location
  # > to load the challange experience from the island center (bedrock).
  set {_bedrock} to {SB::player::%{_uuid}%::island::bedrock}
  set {_x} to x-coord of {_bedrock}
  set {_y} to y-coord of {_bedrock}
  set {_z} to z-coord of {_bedrock}
  set {_xp} to {SB::island::%{_x}%_%{_y}%_%{_z}%::challengexp}
  if {_xp} is not set:
    set {_xp} to 0
  #
  # > Get the current level.
  loop 5 times:
    if {_xp} >= getconfigobject("challenges-config-threshold-%{_challengelvl}%"): 
      set {_currentlevel} to {_challengelvl}
    add 1 to {_challengelvl}
  set {_challengelvl} to 1
  #
  # > base64 encoded skin urls for menu design.
  set {_lvlhead::1} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZDJhNmYwZTg0ZGFlZmM4YjIxYWE5OTQxNWIxNmVkNWZkYWE2ZDhkYzBjM2NkNTkxZjQ5Y2E4MzJiNTc1In19fQ=="
  set {_lvlhead::2} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvOTZmYWI5OTFkMDgzOTkzY2I4M2U0YmNmNDRhMGI2Y2VmYWM2NDdkNDE4OWVlOWNiODIzZTljYzE1NzFlMzgifX19"
  set {_lvlhead::3} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvY2QzMTliOTM0M2YxN2EzNTYzNmJjYmMyNmI4MTk2MjVhOTMzM2RlMzczNjExMWYyZTkzMjgyN2M4ZTc0OSJ9fX0="
  set {_lvlhead::4} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZDE5OGQ1NjIxNjE1NjExNDI2NTk3M2MyNThmNTdmYzc5ZDI0NmJiNjVlM2M3N2JiZTgzMTJlZTM1ZGI2In19fQ=="
  set {_lvlhead::5} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvN2ZiOTFiYjk3NzQ5ZDZhNmVlZDQ0NDlkMjNhZWEyODRkYzRkZTZjMzgxOGVlYTVjN2UxNDlkZGRhNmY3YzkifX19"
  set {_lvlhead::exclamation} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZGQyYWNkOWYyZGZjMmUwNWY2OWQ5NDFmZTk5NzBlOGMzZjA1NTI3YTAyYTkzODExNTc4OTFjOGRkYjhjZjMifX19"
  set {_lvlheadactive::1} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYjk0OWRmMzZhMWEzZjdjYjRjNjcwNjVhOWQ1MzUwMjU4YTNjNTFiMDJhMWEzNzdiODRhODI4NzZkNzdiIn19fQ=="
  set {_lvlheadactive::2} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNThjY2ExZTBmYjVkOWJmNzdmNTI3MzU2ZThiZjRlNTNjYjRhNGM1NmYxMWU3NzlhYmFkYWU1NDFiYmVkYzYifX19"
  set {_lvlheadactive::3} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvZmU5MDc2NzJhNDcwOTM4NTI1MDU4YzBlYTMxOWQ4YmU1YmEyOGQ5NzJjNTJmMmJkNTVkYmY0MTgyYjgifX19"
  set {_lvlheadactive::4} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYWYyZDI2MDFmOWMxYWNlZmZjYzgyMzY1N2UyMjU5ZDM1OGJiNTJiZDQ2MzEyYmYxYzc2ZWUyM2QzYjE3In19fQ=="
  set {_lvlheadactive::5} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYjc5NzM1MzdiZDIxNjJhNzhhNTg2Mzk3YTFlZGE4NTk5ZjRjOTBkYzExYTFlN2Q2NTUyODg0ODM0MjkyNTFiZCJ9fX0="
  set {_lvlheadactive::exclamation} to "eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvOWI5MzRhYjk0YjU2YzM0OGFkYTQyZTIxMjZmNGY5NzQzMjkxYzJiNmVmNDQ5ZGM0ZTM4MzE1ZGM3YjFhYyJ9fX0="
  #
  # > Get island challenge data for repeats.
  set {_islandchallengedata} to {SB::island::%{_x}%_%{_y}%_%{_z}%::challenges}
  set {_islandchallengedata::*} to {_islandchallengedata} split at ","
  loop {_islandchallengedata::*}:
    set {_tdata::*} to loop-value split at "="
    set {_tdata::2} to {_tdata::2} parsed as integer
    set {_finalchallenges::%{_tdata::1}%} to {_tdata::2}
  #
  # > Add challenge status head.
  set {_lore} to getlang("challenge_statssitelore",{_lang})
  set {_name} to getlang("robot_stats",{_lang})
  if {_page} is 0:
    #
    # > Add status data if the page is 0. Here are no challenges rendered.
    # > Currently only used to display the XP of the player. Here could be
    # > added more, like a store where XP can be exchanged in something.
    set {_item} to getcustomhead("yellowexclamation", "%{_lvlheadactive::exclamation}%")
    setguiitem({_p},45,{_item},1,"&r%{_name}%","&r%{_lore}%"," ",false)
    set {_xplang} to getlang("challenge_xp",{_lang})
    replace all "<amount>" with "%{_xp}%" in {_xplang}
    setguiitem({_p},22,{_p}'s skull,1,getlang("robot_stats",{_lang},"&r"),"%{_xplang}%"," ",false)
    set {_repeatbonus} to getcurrentrepeats({_xp},100)
    remove 100 from {_repeatbonus}
    #
    # > Calculate repeat statistic, if enabled.
    set {_repeatbonusenabled} to getconfigobject("challenges-config-repeatmodifier-enabled")
    if {_repeatbonusenabled} is true:
      set {_neededxp} to getconfigobject("challenges-config-repeatmodifier-xprepeatincrease")
      set {_repeatincrease} to getconfigobject("challenges-config-repeatmodifier-repeatincrease")
      set {_diffincrease} to getconfigobject("challenges-config-repeatmodifier-difficulty")
      set {_repeatmodifieritem} to getconfigobject("challenges-config-repeatmodifier-item")
      set {_startrepeats} to 100
      #
      # > While loop as long we're not done.
      while {_done} is not set:
        #
        # > If the experience is equal or bigger than the needed xp,
        # > increase the repeats and needed xp counter.
        if {_xp} >= {_neededxp}:
          set {_neededxp} to (({_neededxp}/100)*{_diffincrease})+{_neededxp}
          set {_repeats} to (({_repeats}/100)*{_repeatincrease})+{_repeats}
        #
        # > If there is not enough experience, we're done here, return.
        else:
          set {_done} to true
      #
      # > Calculate the progress in percent until the new repeat bonus is hit.
      set {_progress} to ({_xp}/{_neededxp})*100
      #
      # > Display the repeat bonus statistic item.
      getlang("challenge_repeatbonustitle",{_lang})
      set {_lore} to getlang("challenge_repeatbonuslore",{_lang})
      replace all "<bonus>" with "%{_repeatbonus}%" in {_lore}
      replace all "<progress>" with "%{_progress}%" in {_lore}
      setguiitem({_p},24,{_repeatmodifieritem},1,getlang("challenge_repeatbonustitle",{_lang}),{_lore}," ",false)
  else:
    set {_item} to getcustomhead("blackexclamation", "%{_lvlhead::exclamation}%")
    setguiitem({_p},45,{_item},1,"&r%{_name}%","&r%{_lore}%","openchallenges(""%{_p}%"" parsed as player,0)",false)
  #
  # > Display all 5 challenge level heads and add click triggers for them.
  loop 5 times:
    set {_lvltranslation} to getlang("challenge_level",{_lang})
    replace all "<level>" with "%loop-number%" in {_lvltranslation}
    set {_slot} to 46 + loop-number
    if {_page} is loop-number:
      #
      # > Get a custom head to display it for the current level (selected level).
      set {_item} to getcustomhead("yellowint%loop-number%", "%{_lvlheadactive::%loop-number%}%")
      set {_lore} to getlang("challenge_unlocked",{_lang})
      setguiitem({_p},{_slot},{_item},1,"&r%{_lvltranslation}%","&r%{_lore}%"," ",false)
      #
      # > Render the challenge items in with the renderchallengeitems function.
      renderchallengeitems({_p},{_page},{_xp})
    else:
      #
      # > Get a custom head to display it for the current level (not selected).
      set {_item} to getcustomhead("blackint%loop-number%", "%{_lvlhead::%loop-number%}%")
      if {_currentlevel} >= loop-number:
        #
        # > The level is unlocked, make it clickable and name it properly.
        set {_lore} to getlang("challenge_unlocked",{_lang})
        replace all "||" with "\n" in {_lore}
        setguiitem({_p},{_slot},{_item},1,"&r%{_lvltranslation}%","&r%{_lore}%","openchallenges(""%{_p}%"" parsed as player,%loop-number%)",false)
      else:
        #
        # > The level has to be unlocked first, add a specific lore for that and make it unclickable.
        set {_lore1} to getlang("challenge_xpneeded",{_lang})
        set {_lore2} to getlang("challenge_xpionlywclvl",{_lang})
        set {_thresholdamount} to getconfigobject("challenges-config-threshold-%loop-number%")
        set {_lore} to "%{_lore1}%||%{SB::config::guispacer}%||&r%{_xp}%/%{_thresholdamount}%||%{SB::config::guispacer}%||%{_lore2}%"
        replace all "||" with "\n" in {_lore}
        setguiitem({_p},{_slot},{_item},1,"&r%{_lvltranslation}%","&r%{_lore}%"," ",false)
  #
  # > Set a item to go back to the previous menu.
  setguiitem({_p},53,{SB::config::backguiitem},1,getlang("guibacktopreviousmenu",{_lang}),getlang("guibacktopreviousmenulore",{_lang}),"make ""%{_p}%"" parsed as player execute command ""/is""",true)

#
# > Function - renderchallengeitems
# > Parameters:
# > <player>the player who should get the challenge items rendered
# > <number>the challenge level (page) of the menu
# > <number>the current challenge xp
# > Actions:
# > Renders the challenge items for the defined player and the challenge level.
function renderchallengeitems(player:player,page:number,currentxp:number=0):
  #
  # > Get data to load island challenge data.
  set {_uuid} to uuid of {_player}
  set {_bedrock} to {SB::player::%{_uuid}%::island::bedrock}
  set {_x} to x-coord of {_bedrock}
  set {_y} to y-coord of {_bedrock}
  set {_z} to z-coord of {_bedrock}
  set {_xp} to {SB::island::%{_x}%_%{_y}%_%{_z}%::challengexp}
  if {_xp} is not set:
    set {_xp} to 0
  #
  # > Get island challenge data for repeats.
  set {_islandchallengedata} to {SB::island::%{_x}%_%{_y}%_%{_z}%::challenges}
  set {_islandchallengedata::*} to {_islandchallengedata} split at ","
  loop {_islandchallengedata::*}:
    set {_tdata::*} to loop-value split at "="
    set {_tdata::2} to {_tdata::2} parsed as integer
    set {_finalchallenges::%{_tdata::1}%} to {_tdata::2}
  set {_challangelvlpart} to 1
  set {_slot} to 10
  #
  # > Get local varibales we need, like uuid and language.
  set {_uuid} to uuid of {_player}
  set {_lang} to getlangcode({_player})
  loop 21 times:
    add 1 to {_loops}
    if {_loops} is 8:
      set {_loops} to 1
      add 2 to {_slot}
    if getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_item") is set:
      set {_item} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_item")
    #
    # > If the display item isn't set, stop rendering here.
    else:
      stop loop
    #
    # > Extract needed items for this challenge:
    set {_needed} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_needed")
    set {_needed::*} to {_needed} split at ","
    #
    # > Loop trough all needed items and create a lore compatible text:
    set {_loreneeded} to getlang("challenge_youneed",{_lang})
    loop {_needed::*}:
      set {_tmp::*} to loop-value-2 split at "-"
      set {_loreneeded} to "%{_loreneeded}%\n"
      set {_m} to getlang("challenge_amountofitem",{_lang})
      replace all "<amount>" with "%{_tmp::1}%" in {_m}
      if {SB::lang::item::%{_tmp::2}%::%{_lang}%} is not set:
        replace all "<item>" with "%{_tmp::2}%" in {_m}
      else:
        #
        # > Once 1.14 is out, this part is going to be replaced with chat components. 
        replace all "<item>" with "%{SB::lang::item::%{_tmp::2}%::%{_lang}%}%" in {_m}
      set {_loreneeded} to "%{_loreneeded}%%{_m}%"
      #
      # > Extract reward items for this challenge:
      #
      # > If the challenge has already been done, display the repeat rewand.
      set {_finalchallenges::*} to metadata value "finalchallenges" of {_player}
      if {_finalchallenges::%{_page}%%{_challangelvlpart}%} is set:
        set {_done} to true
        set {_reward} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_repeatreward")
      #
      # > The challenge has not been done, display first rewards.
      else:
        set {_done} to false
        set {_reward} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_reward")
      #
      # > Split the rewards string to get a list of rewards.
      set {_reward::*} to {_reward} split at ","
      set {_msg} to getlang("challenge_youget",{_lang})
      set {_lorereward} to "\n%{_msg}%"
      #
      # > Add custom reward text to reward lore.
      if getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_customreward") is set:
        if getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_customreward") is not "":
          set {_customreward} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_customreward")
          set {_lorereward} to "%{_lorereward}%\n%{_customreward}%"
      #
      # > Loop through the reward list.
      loop {_reward::*}:
        #
        # > Each list entry contains a string with <amount>-<itemname>, split it at "-",
        # > to display it correctly.
        set {_tmp::*} to loop-value-3 split at "-"
        set {_lorereward} to "%{_lorereward}%\n"
        #
        # > Get the translation and replace placeholders.
        set {_m} to getlang("challenge_amountofitem",{_lang})
        replace all "<amount>" with "%{_tmp::1}%" in {_m}
        if {SB::lang::item::%{_tmp::2}%::%{_lang}%} is not set:
          replace all "<item>" with "%{_tmp::2}%" in {_m}
        else:
          replace all "<item>" with "%{SB::lang::item::%{_tmp::2}%::%{_lang}%}%" in {_m}
        set {_lorereward} to "%{_lorereward}%%{_m}%"
        #
        # > Repeat information is added at the end of the item lore, a spacer is added to make it easier to read.
        set {_loreend} to "\n%{SB::config::guispacer}%\n"
        if getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_repeat") is true:
          set {_m} to getlang("challenge_repeatable",{_lang})
          set {_c} to 0
          set {_c} to {SB::crepeats::%{_uuid}%-%{_page}%%{_challangelvlpart}%}
          set {_max} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_repeatsperday")
          #
          # > If the server operator set the repeat amount per day to -1, it is endless.
          if {_max} is -1:
            set {_max} to "âˆž"
          #
          # > If tthe current repeats aren't unlimited, calculate the maximum amount.
          else:
            set {_max} to getcurrentrepeats({_xp},{_max})
          replace all "<current>" with "%{_c}%" in {_m}
          replace all "<maxrepeat>" with "%{_max}%" in {_m}
          set {_loreend} to "%{_loreend}%%{_m}%"
          #
          # > If the maximum amount is reached, make it a glowing item.
          if {_c} is {_max}:
            set {_item} to glowing {_item}
        #
        # > If repeats are disabled for the challenge:
        else:
          set {_msg} to getlang("challenge_notrepeatable",{_lang})
          set {_loreend} to "%{_loreend}%%{_msg}%"
          #
          # > Repeats for this challenge are disabled, if it has been done, make it glowing
          if {_done} is true:
            set {_item} to glowing {_item}
      # > Currently, custom syntax isn't always working as intended, use functions until they work better.
      setguiitem({_player},{_slot},{_item},1,getlang("challenge_name_%{_page}%_%{_challangelvlpart}%",{_lang},"&r"),"&r%{_loreneeded}%%{_lorereward}%%{_loreend}%","solvechallenge(""%{_player}%"" parsed as player,""%{_page}%::%{_challangelvlpart}%"",%{_page}%)||solvechallenge(""%{_player}%"" parsed as player,""%{_page}%::%{_challangelvlpart}%"",%{_page}%,true)",false)
    #
	# > The challenge level part loop ends here, add 1 to the slot and challenge level part.
    add 1 to {_slot}
    add 1 to {_challangelvlpart}

#
# > Function: solvechallenge
# > Arguments:
# > <player>player,<text>challenge id (challenge level::challenge level part)
# > Actions:
# > This function gets called, if the player tries to solve a challenge trough
# > the challange menu. If successfull, remove items, add xp and items to player's inventory.
function solvechallenge(p:player,challenge:text,page:number=0,shift:boolean=false):
  #
  # > Convert challenge format to page and challenge level page part.
  set {_challengesplit::*} to {_challenge} split at "::"
  #set {_page} to {_challengesplit::1}
  set {_challangelvlpart} to {_challengesplit::2}
  #
  # > Set needed local variables.
  set {_needed} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_needed")
  set {_reward} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_reward")
  set {_repeatreward} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_repeatreward")
  set {_repeat} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_repeat")
  set {_repeatsperday} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_repeatsperday")
  set {_cid} to {_challenge}
  replace all "::" with "" in {_cid}
  set {_uuid} to uuid of {_p}
  set {_lang} to getlangcode({_p})
  set {_prefix} to getlang("prefix",{_lang})
  set {_bedrock} to {SB::player::%{_uuid}%::island::bedrock}
  set {_x} to x-coord of {_bedrock}
  set {_y} to y-coord of {_bedrock}
  set {_z} to z-coord of {_bedrock}
  #
  # > Get challenge experience and calculate maximum repeats for this challenge.
  set {_xp} to {SB::island::%{_x}%_%{_y}%_%{_z}%::challengexp}
  if {_xp} is not set:
    set {_xp} to 0
  set {_repeatsperday} to getcurrentrepeats({_xp},{_repeatsperday})
  #
  # > Split the string to a array. (challenge level=1,challenge id=3=repeats 5 amount == 13=5)
  # > Get repeat amount: {_finalchallenges::%{_cid}%}
  set {_islandchallengedata} to {SB::island::%{_x}%_%{_y}%_%{_z}%::challenges}
  set {_islandchallengedata::*} to {_islandchallengedata} split at ","
  loop {_islandchallengedata::*}:
    set {_tdata::*} to loop-value split at "="
    set {_tdata::2} to {_tdata::2} parsed as integer
    set {_finalchallenges::%{_tdata::1}%} to {_tdata::2}
  #
  # > Check repeatable challenges here.
  if {_repeat} is true:
    #
    # > If it is not allowed to repeat the challenge unlimited, check if there are already repeats for today.
    if {_repeatsperday} is not -1:
      if {SB::crepeats::%{_uuid}%-%{_cid}%} is set:
        #
        # > If there are more repeats than allowed, print error and stop it.
        if {SB::crepeats::%{_uuid}%-%{_cid}%} >= {_repeatsperday}:
          set {_msg} to getlang("challenge_onlyxtimesaday",{_lang})
          replace all "<amount>" with "%{_repeatsperday}%" in {_msg}
          message "%{_prefix}% %{_msg}%" to {_p}
          close {_p}'s inventory
          stop
  #
  # > Check if this challenge has been done already.
  if {_finalchallenges::%{_cid}%} is set:
    #
    # > If it is not allowed to repeat this challenge, print error and stop.
    if {_repeat} is false:
      set {_msg} to getlang("challenge_cantrepeated",{_lang})
      message "%{_prefix}% %{_msg}%" to {_p}
      close {_p}'s inventory
      stop
    #
    # > If it is allowed to repeat, set the reward to the repeat reward.
    set {_reward} to {_repeatreward}
  #
  # > Extract needed items for this challenge:
  set {_needed::*} to {_needed} split at ","
  #
  # > Loop trough all needed items and create a lore compatible text:
  loop {_needed::*}:
    set {_tmp::*} to loop-value split at "-"
    set {_tmp::1} to {_tmp::1} parsed as number
    set {_tmp::3} to {_tmp::2}
    set {_tmp::2} to {_tmp::2} parsed as item
    #
    # > If the player doesn't have enough items in his inventory.
    if number of {_tmp::2} in {_p}'s inventory < {_tmp::1}:
      #
      # > If the player pressed shift, do not send a error message,
      # > but open the challenges menu again and then stop.
      if {_shift} is true:
        openchallenges({_p},{_page})
        stop
      #
      # >
      if {_incomplete} is not set:
        set {_msg} to getlang("challenge_youstillneed",{_lang})
        message "%{_prefix}% %{_msg}%" to {_p}
      set {_amount} to number of {_tmp::2} in {_p}'s inventory
      set {_msg} to getlang("challenge_amountofitem",{_lang})
      #
      # > Replace the amount of the item, which is already in the inventory and
      # > the amount, that is needed to complete (solve) this challange.
      replace all "<amount>" with "%{_tmp::1} - {_amount}%" in {_msg}
      if {SB::lang::item::%{_tmp::3}%::%{_lang}%} is not set:
        replace all "<item>" with "%{_tmp::3}%" in {_msg}
      else:
        replace all "<item>" with "%{SB::lang::item::%{_tmp::3}%::%{_lang}%}%" in {_msg}
      #
      # > Print error to player that there is something missing to complete this challenge.
      message "%{_prefix}% %{_msg}%" to {_p}
      set {_incomplete} to true
  #
  # > If the player does not have everything in the inventory, stop here.
  if {_incomplete} is true:
    close {_p}'s inventory
    stop
  #
  # > If the player has everything in his inventory, remove it.
  loop {_needed::*}:
    set {_tmp::*} to loop-value split at "-"
    set {_tmp::1} to {_tmp::1} parsed as number
    set {_tmp::2} to {_tmp::2} parsed as item
    remove {_tmp::1} of {_tmp::2} from {_p}'s inventory
  #
  # > Split the reward string and parse the rewards to give it to the player.
  set {_reward::*} to {_reward} split at ","
  loop {_reward::*}:
    set {_tmp::*} to loop-value split at "-"
    set {_tmp::1} to {_tmp::1} parsed as number
    #
    # > If the reward is xp, set the xp variable.
    if {_tmp::2} is "xp":
      set {_xp} to {_tmp::1}
    #
    # > If the reward is not "xp", set it to a item and check inventory.
    else:
      set {_tmp::2} to {_tmp::2} parsed as item
      #
      # > If there is enough space in the inventory for the item, give it the player.
      if {_p} has enough space for {_tmp::1} of {_tmp::2}:
        give {_tmp::1} of {_tmp::2} to {_p}
      #
      # > If there is not enough space in the inventory of the player, spawn it at the location of the player on the floor.
      else:
        set {_droploc} to {_p}'s location
        add 0.5 to y-coord of {_droploc}
        drop {_tmp::1} of {_tmp::2} at {_droploc}

  #
  # > If there is a custom function set for this challenge, execute it with evaluate.
  if getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_customfunction") is set:
    if getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_customfunction") is not "":
      set {_exec} to getconfigobject("challenges-config-c-%{_page}%_%{_challangelvlpart}%_customfunction")
      replace all "<player>" with "%{_p}%" in {_exec}
      evaluate "%{_exec}%"
  #
  # > Add experience to the island of the player.
  if {_xp} is set:
    add {_xp} to {SB::island::%{_x}%_%{_y}%_%{_z}%::challengexp}
  #
  # > Everything is done, add one to repeats for this challenge.
  add 1 to {SB::crepeats::%{_uuid}%-%{_cid}%}
  add 1 to {_finalchallenges::%{_cid}%}
  #
  # > Add this challenge to island score string.
  loop {_finalchallenges::*}:
    if {_i} is not set:
      #
      # > Challenge id=challenge solved amount, eg. 13=5 (challenge level 1, challenge 3 has been solved 5 times)
      set {_infalc} to "%loop-index%=%loop-value%"
      set {_i} to 1
    else:
      #
      # > Split every new challenge with a comma. Add challenges to the string like above.
      set {_infalc} to "%{_infalc}%,%loop-index%=%loop-value%"
  #
  # > Save the new challenge string to the island.
  set {SB::island::%{_x}%_%{_y}%_%{_z}%::challenges} to {_infalc}
  #
  # > Open challenges menu again, if the player didn't press shift.
  # > If the player pressed shift, repeat this challenge.
  if {_shift} is false:
    openchallenges({_p},{_page})
  else:
    solvechallenge({_p},{_challenge},{_page},{_shift})

#
# > Function - changechallengexp
# > Parameters:
# > <offline player>the player who should get the challenge xp changed
# > <text>either "add" or "remove" to add or change the amount
# > <number>the amount which should be added or removed from the challenge xp
# > Actions:
# > Adds or removes the amount from the xp balance. Returns the changed amount if successful.
function changechallengexp(player:offline player,action:text,amount:number) :: number:
  #
  # > Get the uuid of the player to get the current bedrock.
  set {_uuid} to uuid of {_player}
  #
  # > Get the bedrock of the player and split it into the coordinates.
  set {_loc} to {SB::player::%{_uuid}%::island::bedrock}
  set {_x} to x-coord of {_loc}
  set {_y} to y-coord of {_loc}
  set {_z} to z-coord of {_loc}
  #
  # > Add or remove the challenge xp from the island the player is on.
  if {_action} is "add":
    add {_amount} to {SB::island::%{_x}%_%{_y}%_%{_z}%::challengexp}
    return {_amount}
  else if {_action} is "remove":
    remove {_amount} from {SB::island::%{_x}%_%{_y}%_%{_z}%::challengexp}
    return {_amount}

#
# > Function - getcurrentrepeats
# > Parameters:
# > <number>experience of the player
# > <number>repeats of the challenge
# > Actions:
# > Returns the current repeat amount which the player is allowed to do. This
# > is not static, since the amount can increase over the time with increasing
# > challenge experience.
function getcurrentrepeats(experience:number, repeats:number) :: number:
  #
  # > If the repeat bonus is disabled, return the default repeat amount.
  set {_repeatbonusenabled} to getconfigobject("challenges-config-repeatmodifier-enabled")
  if {_repeatbonusenabled} is false:
    return {_repeats}
  #
  # > Shorten the long variables to short local variables.
  set {_neededxp} to getconfigobject("challenges-config-repeatmodifier-xprepeatincrease")
  set {_repeatincrease} to getconfigobject("challenges-config-repeatmodifier-repeatincrease")
  set {_diffincrease} to getconfigobject("challenges-config-repeatmodifier-difficulty")
  set {_startrepeats} to {_repeats}
  #
  # > While loop as long we're not done.
  while {_done} is not set:
    #
    # > If the experience is equal or bigger than the needed xp,
    # > increase the repeats and needed xp counter.
    if {_experience} >= {_neededxp}:
      set {_neededxp} to (({_neededxp}/100)*{_diffincrease})+{_neededxp}
      set {_repeats} to (({_repeats}/100)*{_repeatincrease})+{_repeats}
    #
    # > If there is not enough experience, we're done here, return.
    else:
      set {_done} to true
  add {_startrepeats} to {_repeats}
  delete {_done}
  return round({_repeats})
