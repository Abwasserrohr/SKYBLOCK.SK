#
# ==============
# calcisland.sk
# ==============
# calcisland.sk is part of the SKYBLOCK.SK functions.
# ==============

import:
  java.util.HashMap
  java.lang.Integer

#
# > Function: calcisland
# > Arguments:
# > <offline player>player
# > Actions:
# > Calculates the island level by getting both borders on the island, then looping though all blocks and
# > calculating all predefined experience bonuses from the configuration together to get a level.
function calcisland(p:offline player):
  set {_exp} to 0 
  set {_level} to 1
  send action bar "%{SB::config::color::secondary::1}% Preparing..." to {_p}

  #
  # > Set the experience needed for the next level:
  set {_xpn} to {_level}*{SB::config::explevel}
  #
  # > Get the uuid of the player
  set {_uuid} to uuid of {_p}
  #
  # > Get the location of the bedrock of the island the player is on:
  set {_bedrock} to block at {SB::player::%{_uuid}%::island::bedrock}
  #
  # > Set a local variable for chest and storage unit calculation.
  set {_calculatechest} to yaml value "calcinventory" from "plugins/Skript/scripts/SkyBlock/config/blocks.yml"
  set {_calculatestorage} to yaml value "calcstorage" from "plugins/Skript/scripts/SkyBlock/config/blocks.yml"
  set {_exp} to 0
  #
  # > Calculate, how many blocks have be checked to make a fully working progress bar:
  set {_dist} to {SB::config::distance}
  set {_l} to {_dist}*2
  set {_b} to {_dist}*2
  set {_h} to 256
  set {_size} to {_l}*{_b}*{_h}
  #
  # > Get the border locations of the island to calculate the island level.
  set {_b1} to location of {_bedrock}
  set {_b2} to location of {_bedrock}
  add ({_dist} - {SB::config::protect}) to x-coord of {_b1}
  add ({_dist} - {SB::config::protect}) to z-coord of {_b1}
  subtract ({_dist} - {SB::config::protect}) from x-coord of {_b2}
  subtract ({_dist} - {SB::config::protect}) from z-coord of {_b2}
  #
  # > The first loop only loops the y-coordinate 0.
  set y-coordinate of {_b2} to 0
  set y-coordinate of {_b1} to 0
  #
  # > Set the experience per block into a list and convert it to a usable format for us.
  set {_bxp::*} to yaml list "blocks" from "plugins/Skript/scripts/SkyBlock/config/blocks.yml"
  delete {_blockexp::*}
  loop {_bxp::*}:
    set {_lv} to "%loop-value%"
    replace all "{" with "" in {_lv}
    replace all "}" with "" in {_lv}
    set {_lv::*} to {_lv} split at "="
    set {_blockexp::%{_lv::1}%} to {_lv::2} parsed as number
  #
  # > If the nether is enabled, double the island size, since we have the normal island and the nether island.
  if {SB::config::nether} is true:
    set {_size} to {_size}*2
    set {_loops} to 2
  else:
    set {_loops} to 1
  set {_needed} to 0
  set {_progress} to "%{SB::config::color::secondary::1}%||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||||&r"
  #
  # > If the nether islands are enabled, this function has to loop two times through the two islands (normal and nether).
  loop {_loops} times:
    add 1 to {_looptime}
    if {_looptime} is 2:
      set {_b1} to location at x-coord of {_b1}, y-coord of {_b1}, z-coord of {_b1} in "%{SB::config::netherworld}%" parsed as world
      set {_b2} to location at x-coord of {_b2}, y-coord of {_b2}, z-coord of {_b2} in "%{SB::config::netherworld}%" parsed as world
    #
    # > If there are robots active, check if there are any within,
    # > the island and disable them to calculate properly.
    set {_robotsize} to size of {TMP::activerobo::*}
    if {_robotsize} >= 1:
      loop {TMP::activerobo::*}:
        if isWithinArea(loop-value-2's location,{_b1},{_b2}) is true:
          disablerobot(loop-value-2)

    set {_world} to {_b1}'s world

    #
    # > Force load all needed chunks for a faster calculation process.
    handletempchunks({_p},{_world},true)

	#
	# > Get the minimum and maximum coordinate for each x-, y- and z-coord.
    set {_x1} to getmin(x-coord of block at {_b1}, x-coord of block at {_b2})
    set {_x2} to getmax(x-coord of block at {_b1}, x-coord of block at {_b2})
    set {_y1} to getmin(y-coord of block at {_b1}, y-coord of block at {_b2})
    set {_y2} to getmax(y-coord of block at {_b1}, y-coord of block at {_b2})
    set {_z1} to getmin(z-coord of block at {_b1}, z-coord of block at {_b2})
    set {_z2} to getmax(z-coord of block at {_b1}, z-coord of block at {_b2})
    #
    # > Loop through the island by going through every x- and z-coord and then
    # > looping the y-coordinate trough.
    set {_x} to {_x1}

    while {_x} < {_x2}:
      add 1 to {_x}
      set {_z} to {_z1}
      while {_z} < {_z2}:
        add 1 to {_z}
        add 1 to {_i}
        set {_l1} to location at {_x}, 0, {_z} in {_world}
        set {_l2} to {_l1}
        set y-coord of {_l2} to 256
        set {_chunk} to chunk at {_l1}
        set {_snapshot} to (metadata value "SkyBlock|Islandsnapshot" of {_p}).get("%{_world}%|%{_chunk}.getX()%|%{_chunk}.getZ()%")
        set {_relativeLoc} to convertWorldLocToChunkLoc({_l1})

        set {_y} to 0
        while {_y} < 256:
          delete {_block}
          if {_y} is 0:
            #
            # > If the highest block at Y that is non-air is 0, we
            # > have nothing there, since the first block is at 1.
			# > This allows us to just skip this row.
            set {_check} to {_snapshot}.getHighestBlockYAt({_relativeLoc}.getX(), {_relativeLoc}.getZ())
            if {_check} is 0:
              set {_y} to 257
          if {_y} is not 257:
            set {_blocktype} to "%{_snapshot}.getBlockType({_relativeLoc}.getX(), {_y}, {_relativeLoc}.getZ())%"
            if {_blocktype} is not "AIR":

              #
              # > If there is experience set for this block, add it to the island experience.
              if {_blockexp::%{_blocktype}%} is set:
                #
                # > Add the experience of the block to the local experience variable {_exp}
                add {_blockexp::%{_blocktype}%} to {_exp}

          add 1 to {_y}
        add 256 to {_i}
        add 256 to {_blocks}
        #
        # > Update the status every 50000 blocks.
        if {_i} > 50000:
          set {_percent} to ({_blocks}/{_size})*100
          #
          # > If the percentage is bigger than the one before, replace one of the | progress bars with a colored one.
          if {_percent} >= {_needed}:
            add 1 to {_needed}
            replace all "%{SB::config::color::secondary::1}%|" with "%{SB::config::color::primary::1}%|%{SB::config::color::secondary::1}%" in {_progress}
          #
          # > Send the current progress bar to the player.
          if {_blocks} > {_size}:
            set {_blocks} to {_size}
          send action bar "%{_progress}% %{SB::config::color::secondary::1}%[%{_blocks}%%{SB::config::color::primary::1}%/%{SB::config::color::secondary::1}%%{_size}%]" to {_p}
          #
          # > Set the counter back to 0 to do this again in 5000 blocks.
          set {_i} to 0

  #
  # > Calculate the island level using a while loop.
  while {_endfound} is not set:
    if {_exp} is more than {_xpn}:
      add 1 to {_level}
      set {_xpn} to ({_level} * {SB::config::explevel})
    else:
      set {_endfound} to true
  delete {_endfound}
  #
  # > Save the generated level to the island variable.
  set {_x} to x-coord of {_bedrock}
  set {_y} to y-coord of {_bedrock}
  set {_z} to z-coord of {_bedrock}
  set {SB::island::%{_x}%_%{_y}%_%{_z}%::level} to {_level}
  #
  # > Delete the block list again to clear it out from the ram.
  delete {_bxp::*}
  #
  # > Set level to a list of all island levels.
  set {SB::islvl::%{_x}%_%{_y}%_%{_z}%} to {_level}
  #
  # > Updates the hopper count to the new counted amount of hoppers:
  set {_tmp} to hoppercounter({_p},"update",{_hopper})
  #
  # > Print the new level to the player trough the actionbar.
  wait 5 ticks
  actionload({_p},"&rLevel: &6&l%{_level}%")
  delete {SB::calcstatus::%{_p}%}
  #
  # > Unload all force loaded chunks of that player.
  delete metadata value "SkyBlock|Islandsnapshot" of {_p}

#
# > Function - convertWorldLocToChunkLoc
# > Convertts a world location into a chunk relative location.
# > Parameters:
# > <Location> The location that should be converted.
# > Returns:
# > <Vector> A vector with all relative locations.
function convertWorldLocToChunkLoc(loc:location) :: object:
  set {_blockX} to Integer.valueOf(round(x-coord of {_loc} - 0.5))
  set {_blockY} to Integer.valueOf(round(y-coord of {_loc} - 0.5))
  set {_blockZ} to Integer.valueOf(round(z-coord of {_loc} - 0.5))
  set {_X} to mod({_blockX}, 16)
  set {_Z} to mod({_blockZ}, 16)
  return vector({_X}, {_blockY}, {_Z})

#
# > Function - handletempchunks
# > Parameters:
# > <offline player> the player which island should be calculated.
# > <boolean>force load temp chunks=true,remove force load temp chunks=false
# > Actions:
# > Loops through all blocks between the two location and loads the chunks,
# > with chunk unload prevention, these chunks keep being loaded for a fast
# > island calculation.
function handletempchunks(p:offline player,world:world,load:boolean=true):

  set {_uuid} to uuid of {_p}
  set {_bedrock} to {SB::player::%{_uuid}%::island::bedrock}

  #
  # > Calculate the borders which should be calculated.
  set {_dist} to {SB::config::distance}
  set {_b1} to location of {_bedrock}
  set {_b2} to location of {_bedrock}
  add ({_dist} - {SB::config::protect}) to x-coord of {_b1}
  add ({_dist} - {SB::config::protect}) to z-coord of {_b1}
  subtract ({_dist} - {SB::config::protect}) from x-coord of {_b2}
  subtract ({_dist} - {SB::config::protect}) from z-coord of {_b2}

  #
  # > Get the minimum and maximum of each x-,y- and z-coord.
  set {_x1} to getmin(x-coord of block at {_b1}, x-coord of block at {_b2})
  set {_x2} to getmax(x-coord of block at {_b1}, x-coord of block at {_b2})
  set {_z1} to getmin(z-coord of block at {_b1}, z-coord of block at {_b2})
  set {_z2} to getmax(z-coord of block at {_b1}, z-coord of block at {_b2})

  #
  # > Loop through the whole island and load the chunks and set them force loaded
  # > to prevent them from being unloaded.
  set {_x} to {_x1}
  set metadata value "SkyBlock|Islandsnapshot" of {_p} to new HashMap()
  while {_x} < {_x2}:
    add 1 to {_x}
    set {_z} to {_z1}
    while {_z} < {_z2}:
      add 1 to {_z}
      set {_l1} to location at {_x}, 1, {_z} in {_world}
      set {_chunk} to chunk at block at {_l1}
      (metadata value "SkyBlock|Islandsnapshot" of {_p}).put("%{_world}%|%{_chunk}.getX()%|%{_chunk}.getZ()%", {_chunk}.getChunkSnapshot())
      #
      # > Only load the chunk if it should be loaded.
      #if {_load} is true:
      #  {_chunk}.load()
  delete {_chunk}
